buildscript {
    repositories {
        jcenter()
        mavenCentral()
    }
    dependencies {
        classpath 'org.asciidoctor:asciidoctor-gradle-plugin:1.5.2'
        classpath 'org.asciidoctor:asciidoctorj-pdf:1.5.0-alpha.6'
        classpath 'org.springframework.boot:spring-boot-gradle-plugin:1.2.3.RELEASE'
        //classpath "com.github.samueltbrown:gradle-cucumber-plugin:0.8"
    }
}

apply plugin: 'java'
apply plugin: 'maven'
apply plugin: 'spring-boot'
apply plugin: 'org.asciidoctor.convert'
//apply plugin: "com.github.samueltbrown.cucumber"

version = '1.0'
group = 'io.github.robwin'

tasks.withType(JavaCompile) {
    sourceCompatibility = "1.7"
    targetCompatibility = "1.7"
    options.deprecation = true
    options.encoding = 'UTF-8'
    options.compilerArgs << "-Xlint:unchecked"
}

repositories {
    jcenter()
    mavenCentral()
    maven { url 'https://repo.spring.io/snapshot' }
    maven { url 'http://oss.jfrog.org/artifactory/oss-snapshot-local/' }
}

dependencies {
    compile 'org.springframework.boot:spring-boot-starter-web'
    compile 'org.springframework.boot:spring-boot-starter-actuator'
    compile 'io.springfox:springfox-swagger2:2.0.0'
    testCompile 'org.springframework.boot:spring-boot-starter-test'
    testCompile 'io.springfox:springfox-staticdocs:2.0.0'
    testCompile 'info.cukes:cucumber-spring:1.2.2'
    testCompile 'info.cukes:cucumber-junit:1.2.2'
    testCompile 'junit:junit:4.11'
    testCompile 'org.springframework.restdocs:spring-restdocs:0.1.0.BUILD-SNAPSHOT'
    testCompile 'com.jayway.restassured:spring-mock-mvc:2.4.0'
    testCompile 'com.fasterxml.jackson.module:jackson-module-jsonSchema:2.4.0'
}

ext {
    generatedDocumentation = file('src/docs/asciidoc/generated')
}

asciidoctor {
    dependsOn test
    sources {
        include 'index.adoc'
    }
    backends = ['html5', 'pdf']
    attributes = [
            doctype: 'book',
            toc: 'left',
            toclevels: '2',
            numbered: '',
            sectlinks: '',
            sectanchors: '',
            generated: generatedDocumentation
    ]
}

jar {
    dependsOn asciidoctor
    from ("${asciidoctor.outputDir}/html5") {
        into 'static/docs'
    }
    from ("${asciidoctor.outputDir}/pdf") {
        into 'static/docs'
    }
}

test {
    systemProperty 'org.springframework.restdocs.outputDir', generatedDocumentation
    systemProperty 'io.springfox.staticdocs.outputDir', generatedDocumentation
    outputs.dir generatedDocumentation
}

/*
cucumber {
    formats = ['pretty', 'html:build/cucumber/html', 'json:build/cucumber/cucumber.json','junit:build/cucumber/cucumber.xml']
    featureDirs = ['src/test/resources/features']
    jvmOptions {
        maxHeapSize = '512m'
    }
}
*/

task wrapper(type: Wrapper) {
    gradleVersion = '2.2.1'
}
