buildscript {
    repositories {
        jcenter()
        mavenCentral()
        mavenLocal()
    }
    dependencies {
        classpath 'org.asciidoctor:asciidoctor-gradle-plugin:1.5.2'
        classpath 'org.asciidoctor:asciidoctorj-pdf:1.5.0-alpha.10.1'
        classpath 'org.springframework.boot:spring-boot-gradle-plugin:1.2.7.RELEASE'
        classpath 'io.github.robwin:swagger2markup-gradle-plugin:0.9.1'
    }
}

apply plugin: 'java'
apply plugin: 'maven'
apply plugin: 'spring-boot'
apply plugin: 'org.asciidoctor.convert'
apply plugin: 'io.github.robwin.swagger2markup'

version = '0.9.1'
group = 'io.github.robwin'

tasks.withType(JavaCompile) {
    sourceCompatibility = "1.7"
    targetCompatibility = "1.7"
    options.deprecation = true
    options.encoding = 'UTF-8'
    options.compilerArgs << "-Xlint:unchecked"
}

repositories {
    jcenter()
    mavenCentral()
    maven { url 'https://repo.spring.io/snapshot' }
    maven { url 'http://oss.jfrog.org/artifactory/oss-snapshot-local/' }
    mavenLocal()
}

dependencies {
    compile 'org.springframework.boot:spring-boot-starter-web'
    compile 'io.swagger:swagger-annotations:1.5.0'
    compile 'com.google.guava:guava:18.0'
    testCompile 'io.springfox:springfox-swagger2:2.2.2'
    testCompile 'org.springframework.boot:spring-boot-starter-test'
    testCompile 'io.springfox:springfox-staticdocs:2.2.2'
    testCompile 'junit:junit'
    testCompile 'org.springframework.restdocs:spring-restdocs-mockmvc:1.0.0.RELEASE'
    testCompile 'com.fasterxml.jackson.module:jackson-module-jsonSchema:2.4.0'
    testCompile 'io.github.robwin:assertj-swagger:0.1.2'

}

ext {
    asciiDocOutputDir = file("${buildDir}/asciidoc")
    swaggerOutputDir = file("${buildDir}/swagger")
}

test {
    systemProperty 'io.springfox.staticdocs.outputDir', swaggerOutputDir
}

convertSwagger2markup {
    dependsOn test
    inputDir swaggerOutputDir
    examplesDir asciiDocOutputDir
    pathsGroupedBy io.github.robwin.swagger2markup.GroupBy.TAGS
}

asciidoctor {
    dependsOn convertSwagger2markup
    sources {
        include 'index.adoc'
    }
    backends = ['html5', 'pdf']
    attributes = [
            doctype: 'book',
            toc: 'left',
            toclevels: '3',
            numbered: '',
            sectlinks: '',
            sectanchors: '',
            hardbreaks: '',
            generated: asciiDocOutputDir
    ]
}

jar {
    dependsOn asciidoctor
    from ("${asciidoctor.outputDir}/html5") {
        into 'static/docs'
    }
    from ("${asciidoctor.outputDir}/pdf") {
        into 'static/docs'
    }
}

/*
cucumber {
    formats = ['pretty', 'html:build/cucumber/html', 'json:build/cucumber/cucumber.json','junit:build/cucumber/cucumber.xml']
    featureDirs = ['src/test/resources/features']
    jvmOptions {
        maxHeapSize = '512m'
    }
}
*/

task wrapper(type: Wrapper) {
    gradleVersion = '2.4'
}
